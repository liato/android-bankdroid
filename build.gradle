buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'com.google.collections:google-collections:1.0'
    compile 'com.fasterxml.jackson.core:jackson-core:2.1.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.1.0'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.1.0'
    compile 'org.jsoup:jsoup:1.7.3'
    compile('org.simpleframework:simple-xml:2.7.+') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
}

tasks.withType(Compile) {
    options.encoding = "UTF-8"
}

android {
    compileSdkVersion 17
    buildToolsVersion "17.0.0"

    defaultConfig {
        versionCode 187
        versionName '1.9.6.15-RC3'
        minSdkVersion 7
        targetSdkVersion 10
        packageName 'com.liato.bankdroid'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        instrumentTest.setRoot('tests')
    }

    final Console console = System.console();
    if (console != null) {

        // Building from console
        signingConfigs {
            release {
                storeFile file("release.keystore")
                storePassword console.readLine("\nEnter keystore password: ")
                keyAlias "andmrkt"
                keyPassword console.readLine("Enter key password: ")
            }
        }

    } else {

        // Building from IDE
        signingConfigs {
            release {

            }
        }

    }


    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}
